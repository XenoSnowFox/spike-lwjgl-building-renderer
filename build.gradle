
plugins {
    id 'application'
    id "com.github.johnrengelman.shadow" version "6.0.0" // Uber-JAR builder
}

version = '0.0.0'
mainClassName = 'spike.lwjglbuildingrenderer.Spike'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

project.ext.lwjglVersion = "3.2.3"
project.ext.lwjglNatives = "natives-windows"

repositories {
    mavenCentral()
}

dependencies {
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation project(':module:lwjgl-engine')
    implementation "org.joml:joml:1.10.1"
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-nuklear"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-shaderc"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nuklear::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-shaderc::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

//    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
//    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
//    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    dependsOn 'copyGameResources'
}

test {
    useJUnitPlatform()
}

task copyGameResources(type: Copy) {
    from("${project.projectDir}/data") {
        include '**/*.*'
    }
    into 'build/libs/data'
}

def gitSubmoduleInit() {
    System.out.println("  > git submodule update --init --recursive");
    exec {
        executable "git"
        args "submodule", "update", "--init", "--recursive"
    }
}

def gitSubmoduleUpdate() {
    System.out.println("  > git submodule update --remote --merge");
    exec {
        executable "git"
        args "submodule", "update", "--remote", "--merge"
    }
}

task initializeSubmodules {
    group 'git'
    doLast {
        gitSubmoduleInit();
    }
}

task updateSubmodules {
    group 'git'
    doLast {
        gitSubmoduleUpdate();
    }
}